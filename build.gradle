apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

repositories {
   maven {
      url 'https://repo1.maven.org/maven2'
   }
   flatDir {
       dirs 'lib'
   }
}

group = 'com.newrelic.instrumentation'
version = '1.4'

dependencies {
  // If using older agent JARs to compile
  compile fileTree(dir: 'lib', includes: ['*.jar'])
      
  // 3rd party jars needed to compile:
  compile 'javax.servlet:servlet-api:2.5'
}

jar {
  manifest {
       attributes 'Implementation-Title': project.group + '.' + project.name, 'Implementation-Vendor-Id': 'Field Instrumentation', 'Implementation-Version': version
  }
}

task install(dependsOn: jar, type: Copy) {
    description = 'Copies compiled jar to the NEW_RELIC_EXTENSIONS_DIR.'
    group  = 'New Relic'

    def extDir = System.getenv("NEW_RELIC_EXTENSIONS_DIR") ?: " "

    from jar
    into extDir
}

install.doFirst  {
    def extDir = System.getenv("NEW_RELIC_EXTENSIONS_DIR")
     if (extDir == null) {
         throw new Exception("Must set NEW_RELIC_EXTENSIONS_DIR.")
     }

     if (extDir.startsWith("~" + File.separator)) {
         extDir = System.getProperty("user.home") + extDir.substring(1);
     }

     if (!file(extDir).directory) {
         throw new Exception(extDir + "NEW_RELIC_EXTENSIONS_DIR, set as '" + extDir + "'is not a valid directory.")
     }
}
